= Game of Thrones GraphGist

    Because winter is coming

:author: Ivan Mosiev, George Martin ;)
:twitter: @polny_otec

//console

== Prologue

[source,cypher]
----
CREATE (NightsWatch:organization{name: "Night's Watch"})
CREATE (HouseRoyce:house{name: 'Royce'})
CREATE (WaymarRoyce:character:knight:noble:ranger{name:'Waymar'})-[h1:from_house]->(HouseRoyce)
CREATE (WaymarRoyce)-[s1:serves]->(NightsWatch)
CREATE (Will:caharacter:ranger:pointOfView{name: 'Will'})-[s2:serves]->(NightsWatch)
CREATE (Gared:caharacter:ranger { name: 'Gared' })-[s3:serves]->(NightsWatch) 
CREATE (Gared)<-[c1:commands]-(WaymarRoyce)-[c2:commands]->(Will)
CREATE (Others:race{name: "Others", also_known_as: ["White Walkers"]})
CREATE (Gared)<-[k1:kill{how: "in battle"}]-(Others)-[k2:kill{how: "in battle"}]->(WaymarRoyce)
CREATE (Will)-[d1:desert]->(NightsWatch)
RETURN h1, s1, s2, s3, c1, c2, Will, Gared, WaymarRoyce, Others, k1, k2, d1
----

//graph

== Bran 1

[source,cypher]
----
CREATE (BranStark:character:pointOfView:noble:greensight:skinchanger{name: "Bran"})-[h2:from_house]->(HouseStark:house{name: "Stark"})
CREATE (HouseStark)<-[hd1:head_of]-(LordEddardStark:character:pointOfView:noble{name: "Eddard", also_known_as: ["Ned"]})-[h3:from_house]->(HouseStark)
CREATE (theNorth:kingdom{name: "North"})<-[w1:warden_of]-(LordEddardStark)-[l1:lord_of]->(Winterfell:castle{name: "Winterfell"})
CREATE (RobbStark:character:noble{name: "Robb"})-[h1:heir_of]->(LordEddardStark)
CREATE (BranStark)<-[f1:father_of]-(LordEddardStark)-[f2:father_of]->(RobbStark)
CREATE (OldNan:character{name: "Nan", also_known_as: ["Old Nan"]})-[s4:serves]->(HouseStark)
CREATE (RobbStark)<-[n1:nurse]-(OldNan)-[n2:nurse]->(BranStark)
CREATE (ManceRayder:character:wildling{name: "Mance", also_known_as: ["Mance Rayder", "King-Beyond-the-Wall"]})
RETURN BranStark, RobbStark, LordEddardStark, theNorth, Winterfell, OldNan
----

[source,cypher]
----
MATCH (LordEddardStark:character:pointOfView:noble{name: "Eddard"}), (Will:caharacter:ranger:pointOfView{name: 'Will'})
CREATE (LordEddardStark)-[k3:kill{how: "execution"}]->(Will)
RETURN LordEddardStark, Will
----

//graph
