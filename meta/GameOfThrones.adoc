= Game of Thrones GraphGist

Because winter is coming

:author: Ivan Mosiev, George Martin ;)
:twitter: @polny_otec

//console

== Prologue

[ source,cypher ]
----
CREATE ( NightsWatch:organization { name: "Night's Watch" } )
CREATE ( HouseRoyce:house { name: 'Royce' } )
CREATE ( WaymarRoyce:character:knight:noble { name:'Waymar' } )-[ :from_house ]->( HouseRoyce )
CREATE ( WaymarRoyce )-[ :serves { as: "ranger" } ]->( NightsWatch )
CREATE ( Will:caharacter:pointOfView {name: 'Will' } )-[ :serves { as: "ranger" } ]->( NightsWatch )
CREATE ( Gared:caharacter { name: 'Gared' } )-[ :serves { as: "ranger" } ]->( NightsWatch )
CREATE ( WaymarRoyce )-[ :commands ]->( Will )
CREATE ( WaymarRoyce )-[ :commands ]->( Gared )
CREATE ( Others:race { name: "Others", also_known_as: [ "White Walkers" ] } )
CREATE ( Others )-[ :kill { how: "in battle" } ]->( WaymarRoyce )
CREATE ( Others )-[ :kill { how: "in battle" } ]->( Will )
CREATE ( Gared )-[ :deserts ]->( NightsWatch )
RETURN Will, Gared, WaymarRoyce, Others
----

//graph

== Bran 1

[ source,cypher ]
----
CREATE ( BranStark:character:pointOfView:noble:greensight:skinchanger { name: "Bran" } )-[ :from_house ]->( HouseStark:house { name: "Stark" } )
CREATE ( LordEddardStark:character:pointOfView:noble { name: "Eddard", also_known_as: [ "Ned" ] } )-[ :from_house ]->( HouseStark )
CREATE ( LordEddardStark )-[ :head ]->( HouseStark )
CREATE ( LordEddardStark )-[ :warden ]->( theNorth:kingdom { name: "North" } )
CREATE ( LordEddardStark )-[ :lord ]->( Winterfell:castle { name: "Winterfell" } )
CREATE ( RobbStark:character:noble { name: "Robb" } )-[ :heir ]->( LordEddardStark )
CREATE ( LordEddardStark )-[ :father ]->( RobbStark )
CREATE ( LordEddardStark )-[ :father ]->( BranStark )
CREATE ( OldNan:character { name: "Nan", also_known_as: [ "Old Nan" ] } )-[ :serves { as: "nurse" } ]->( HouseStark )
CREATE ( Desmond:character { name: "Desmond" } )-[ :serves { as: "household guard" } ]->( HouseStark )
CREATE ( Harwin:character { name: "Harwin" } )-[ :serves { as: "household guard" } ]->( HouseStark )
CREATE ( Hullen:character { name: "Hullen" } )-[ :serves { as: "groom", rank: "Master of Horse" } ]->( HouseStark )
CREATE ( Hullen )-[ :father ]->( Harwin )
CREATE ( JoryCassel:character:noble { name: "Jory" } )-[ :from_house ]->( HouseCassel:house { name: "Cassel" } )
CREATE ( JoryCassel )-[ :serves { as: "household guard", rank: "Captain of the Guards" } ]->( HouseStark )
CREATE ( ManceRayder:character:wildling { name: "Mance", also_known_as: [ "Mance Rayder", "King-Beyond-the-Wall" ] } )
CREATE ( TheonGreyjoy:character:noble:pointOfView { name: "Theon" } )-[ :ward ]->( LordEddardStark )
CREATE ( JonSnow:character:pointOfView:skinchanger { name: "Jon" } )-[ :bastard ]->( LordEddardStark )
CREATE ( LordEddardStark )-[ :father ]->( JonSnow )
CREATE ( Ghost:direwolf { name: "Ghost" } )-[ :bound ]->( JonSnow )
CREATE ( GreyWind:direwolf { name: "Grey Wind" } )-[ :bound ]->( RobbStark )
CREATE ( Summer:direwolf { name: "Summer" } )-[ :bound ]->( BranStark )
RETURN BranStark, RobbStark, LordEddardStark, theNorth, Winterfell, OldNan
----

[ source,cypher ]
----
START LordEddardStark=node:node_auto_index( name="Eddard" ), Gared=node:node_auto_index( name='Gared' )
CREATE ( LordEddardStark )-[ :kill { how: "execution" } ]->( Gared )
RETURN LordEddardStark, Gared
----

//graph

=== Frags list so far

[ source,cypher ]
----
MATCH (victim)<-[k:kill]-(killer)  OPTIONAL
MATCH (victim)-[:from_house]->(victimsHouse)  OPTIONAL
MATCH (killer)-[:from_house]->(killersHouse)
RETURN victim.name + ' ' + COALESCE(victimsHouse.name, '') as `Dead Character`, k.how as `How killed`, killer.name + ' ' + COALESCE(killersHouse.name, '') as `By whom`
----

//table

=== Neo, do you remember what is the name of bastard and his direwolf?

[ source,cypher ]
----
MATCH ( )<-[ :bastard ]-( person )<-[ :bound ]-( direwolf )
RETURN person.name, direwolf.name
----

//table


=== Who serves as household guard of Stark

[source,cypher]
----
START HouseStark = node:node_auto_index(name = "Stark") 
MATCH (HouseStark)<-[s:serves]-(guard) 
WHERE s.as = "household guard"  
OPTIONAL MATCH (guard)-[:from_house]->(house) 
RETURN guard.name + ' ' + COALESCE(house.name, ''), COALESCE(s.rank, 'guard')
----

//table

